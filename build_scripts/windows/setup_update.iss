; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Serial Communication Manager"
#define MyAppVersion "1.0"
#define MyAppPublisher "Aruba"
#define MyAppExeName "serclient.exe"
#define MyAppININame "serclient.ini"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{8ABED5F3-1885-4979-B405-19BB02000A64}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={pf}\Serial Manager
DisableDirPage=yes
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
Compression=lzma
SolidCompression=yes
UpdateUninstallLogAppName=no
UsePreviousAppDir=yes
CreateUninstallRegKey=no
Uninstallable=yes
OutputBaseFilename=SetupUpdate

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "..\..\serclient.version"; DestDir: "{commonappdata}\{#MyAppName}"; Flags: ignoreversion
Source: "output_update\*"; DestDir: "{commonappdata}\{#MyAppName}"; Flags: ignoreversion recursesubdirs createallsubdirs

[Icons]
Name: "{group}\Log"; Filename: "{commonappdata}\{#MyAppName}\log.txt"
Name: "{group}\Configuration INI"; Filename: "{commonappdata}\{#MyAppName}\{#MyAppININame}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[INI]
Filename: "{commonappdata}\{#MyAppName}\{#MyAppININame}"; Section: "LOG"; Flags: createkeyifdoesntexist; Key: "file"; String: "{commonappdata}\{#MyAppName}\log.txt"
Filename: "{commonappdata}\{#MyAppName}\{#MyAppININame}"; Section: "LOG"; Flags: createkeyifdoesntexist; Key: "level"; String: "10"
Filename: "{commonappdata}\{#MyAppName}\{#MyAppININame}"; Section: "SERIAL"; Flags: createkeyifdoesntexist; Key: "port"; String: "COM2"
Filename: "{commonappdata}\{#MyAppName}\{#MyAppININame}"; Section: "SERIAL"; Flags: createkeyifdoesntexist; Key: "baudrate"; String: "57600"
Filename: "{commonappdata}\{#MyAppName}\{#MyAppININame}"; Section: "PLUGINS"; Flags: createkeyifdoesntexist; Key: "root"; String: "{commonappdata}\{#MyAppName}"

[Run]
Filename: "{commonappdata}\{#MyAppName}\{#MyAppExeName}"; Parameters: "--startup auto install"; StatusMsg: "Updating service..."; Flags: waituntilterminated
Filename: "{sys}\sc.exe"; Parameters: "failure SerialService actions= restart/5000/restart/5000/restart/5000 reset= 5000"; StatusMsg: "Setting service failure timings..."; Flags: waituntilterminated
Filename: "{sys}\sc.exe"; Parameters: "start SerialService"; StatusMsg: "Running..."; Flags: waituntilterminated

[Code]
function PrepareToInstall(var NeedsRestart: Boolean): String;
var
  ResultCode: integer;
begin
  Exec(ExpandConstant('{sys}\sc.exe'), 'stop SerialService', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
  Result := '';
end;

function InitializeUninstall():boolean;
var
  ResultCode: integer;
begin
  Exec(ExpandConstant('{sys}\sc.exe'), 'stop SerialService', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
  Exec(ExpandConstant('{sys}\sc.exe'), 'delete SerialService', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
  Result := True;
end;
